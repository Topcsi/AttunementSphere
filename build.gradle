buildscript {
    dependencies {
        classpath 'org.sonarsource.scanner.gradle:sonarqube-gradle-plugin:3.0'
    }

    repositories {
        mavenLocal()
        jcenter()
    }
}

plugins {
    id 'java'
    id 'jacoco'
    id 'maven-publish'
}

apply from: './gradle/commons.qa.gradle'

wrapper {
    gradleVersion = '6.6.1'
    distributionType = Wrapper.DistributionType.ALL
    distributionUrl = "https://services.gradle.org/distributions/gradle-6.6.1-all.zip"
}

compileJava {
    options.release = 8
}

group 'org.dwemer'
version '0.1'

repositories {
    jcenter()
    mavenCentral()
}

dependencies {
    compile 'net.dv8tion:JDA:4.2.0_204'

    compile group: 'org.apache.logging.log4j', name: 'log4j-slf4j-impl', version: '2.13.3'

    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile group: 'org.mockito', name: 'mockito-core', version: '3.5.10'
}

jacocoTestReport {
    dependsOn test // tests are required to run before generating the report
}

jacoco {
    toolVersion = "0.8.5"
    reportsDir = file("$buildDir/jacocoReports")
    jacocoTestReport {
        reports {
            xml.enabled true
        }
    }
}

def ref = System.getenv('GITHUB_REF')
if (ref != null && ref.startsWith('refs/tags/v')) {
    version = ref.substring('refs/tags/v'.length())
}
else {
    // snapshot version with incrementing build number
    String buildNumber = System.getenv('GITHUB_RUN_NUMBER') ?: 'SNAPSHOT'
    version = version + '.' + buildNumber
}

publishing {
    repositories {
        maven {
            url = "https://maven.pkg.github.com/topcsi/attunementsphere"
            credentials {
                username = findProperty('repoUser') ?: System.env.GITHUB_USER
                password = findProperty('repoPass') ?: System.env.GITHUB_TOKEN
            }
        }
    }
    publications {
        gpr(MavenPublication) {
            from(components.java)
        }
    }
}
